#! /usr/bin/env raku

unit sub MAIN (Str $tree = "1 | 2 3 | 4 5 6 7", :$dashes);

my $space = $dashes ?? "-" !! " ";

my @btree = $tree.split("|");

my $rows = @btree.elems;
my $row  = $rows -1;
my $count = 1;

my $indents := (0, { ($_ * 2) +1 } ... *);
my $indent   = $indents[$row];
my $spacing  = 0;

for @btree -> $level
{ 
  my @values = $level.words;

  die "wrong number of element on row { $rows - $row }: { @values.elems } (should have been $count)"
   
    if @values.elems != $count;

  print $space x $indent;

  say @values.join($space x $spacing);

  last if $row == 0;

  $row--;
  $spacing = $indent;
  
  $indent = $indents[$row];

  print $space x $indent;
  print "/{ $space x $spacing}\\{ $space x $spacing }" x $count;
  say "";
  
  $count *= 2;
}
