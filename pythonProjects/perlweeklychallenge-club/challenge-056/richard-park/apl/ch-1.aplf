 Diff_K←{
     ⎕IO←0
     diff←⍵=∘.-⍨⍺                      ⍝ k = A[i]-A[j] for all i,j
⍝ Basic case: k≠0 and ⍺ is a vector (list)
⍝ Example:
⍝       2 7 9 Diff_K 2
⍝ 2 1
     (⍵≠0)∧(1=≢⍴⍺):↑⍸diff
⍝ General case
⍝ Example:
⍝       (2 2⍴⍳4) Diff_K 2
⍝ ┌───┬───┐
⍝ │1 0│0 0│
⍝ ├───┼───┤
⍝ │1 1│0 1│
⍝ └───┴───┘
     ij←(1@(,(2×≢⍴⍺)∘⍴¨⍳⍴⍺))0⍴⍨⍴diff   ⍝ 1 where i=j
     d←↑⍸diff×~ij                      ⍝ Diff_K index pairs
     spli2←{(s⍴1↑⍨2÷⍨s←≢⍵)⊂⍵}          ⍝ Split even length vector in 2
     spli2⍤1⊢d                         ⍝ Split each index pair
 }
